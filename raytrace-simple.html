<script id="vertexShader" type="bogus">
precision highp float;

attribute vec2 vertex;
varying vec2 oTexCoord;

void main() {
  oTexCoord = 0.5 + 0.5 * vertex;
  gl_Position = vec4(vertex, 0.0, 1.0);
}
</script>

<script id="fragmentShader" type="bogus">
#extension GL_OES_standard_derivatives : enable
precision highp float;

uniform vec3 eye;
uniform vec3 axisX;
uniform vec3 axisY;
uniform vec3 axisZ;
varying vec2 oTexCoord;

// Values that were scattered throughout the Oculus world demo
const vec4 HmdWarpParam = vec4(1.0, 0.22, 0.24, 0.0); // For the 7-inch device
const vec4 ChromAbParam = vec4(0.996, -0.004, 1.014, 0.0);
const float HMD_HResolution = 1280.0;
const float HMD_VResolution = 800.0;
const float HMD_HScreenSize = 0.14976;
const float HMD_VScreenSize = HMD_HScreenSize / (HMD_HResolution / HMD_VResolution);
const float HMD_InterpupillaryDistance = 0.064;
const float HMD_LensSeparationDistance = 0.0635;
const float HMD_EyeToScreenDistance = 0.041;
const float lensOffset = HMD_LensSeparationDistance * 0.5;
const float lensShift = HMD_HScreenSize * 0.25 - lensOffset;
const float Distortion_XCenterOffset = 4.0 * lensShift / HMD_HScreenSize;
const float DistortionFitX = -1.0;
const float DistortionFitY = 0.0;
const float stereoAspect = 0.5 * HMD_HResolution / HMD_VResolution;
const float dx = DistortionFitX - Distortion_XCenterOffset;
const float dy = DistortionFitY / stereoAspect;
const float fitRadiusSquared = dx * dx + dy * dy;
const float Distortion_Scale =
  HmdWarpParam.x +
  HmdWarpParam.y * fitRadiusSquared +
  HmdWarpParam.z * fitRadiusSquared * fitRadiusSquared +
  HmdWarpParam.w * fitRadiusSquared * fitRadiusSquared * fitRadiusSquared;

// This samples from a single unwarped [0,0]x[1,1] box containing two views
// side-by-side that have been rendered using normal perspective projection.
// The left eye takes up [0,0]x[0.5,1] and the right eye takes up [0.5,0]x[1,1].
vec4 sample(vec2 point, vec3 eye, vec2 LensCenter) {
  // Compute the ray
  float dx = (point.x - LensCenter.x) * 2.0;
  float dy = point.y - LensCenter.y;
  vec3 ray = axisX * dx + axisY * dy + axisZ;

  // Trace the ray
  float t = -eye.y / ray.y;
  if (t < 0.0) return vec4(0.0);
  vec2 onPlane = eye.xz + ray.xz * t;

  // Magical anti-aliased checkerboard
  vec2 checkerboard = fract(onPlane * 0.5 + 0.25) - 0.5;
  vec2 size = fwidth(onPlane);
  vec2 lines = abs(fract(onPlane) - 0.5) / (size + size * size * size);
  float antialiasing = min(lines.x, lines.y);
  float color = max(0.0, checkerboard.x * checkerboard.y < 0.0 ? 0.5 + antialiasing : 0.5 - antialiasing);
  return vec4(color);
}

void main() {
  // Compute the viewport size
  bool isRight = oTexCoord.x > 0.5;
  float x = isRight ? 0.5 : 0.0;
  float y = 0.0;
  float w = 0.5;
  float h = 1.0;

  // Set up values for the shader
  float XCenterOffset = isRight ? -Distortion_XCenterOffset : Distortion_XCenterOffset;
  vec2 LensCenter = vec2(x + (w + XCenterOffset * 0.5) * 0.5, y + h * 0.5);
  vec2 ScreenCenter = vec2(x + w * 0.5, y + h * 0.5);
  float scaleFactor = 1.0 / Distortion_Scale;
  vec2 Scale = vec2(w * 0.5 * scaleFactor, h * 0.5 * scaleFactor * stereoAspect);
  vec2 ScaleIn = vec2(2.0 / w, 2.0 / h / stereoAspect);

  // Compute the warp
  vec2 theta = (oTexCoord - LensCenter) * ScaleIn;
  float rSq = dot(theta, theta);
  vec2 theta1 = theta * dot(HmdWarpParam, vec4(1.0, rSq, rSq * rSq, rSq * rSq * rSq));
  vec2 tc = LensCenter + theta1 * Scale;

  // Trace the warped ray
  vec3 localEye = eye + axisX * HMD_InterpupillaryDistance * 0.5 * (isRight ? 1.0 : -1.0);
  gl_FragColor = sample(tc, localEye, LensCenter);
}
</script>

<body style="position:fixed;left:0;top:0;right:0;bottom:0;overflow;hidden;margin:0;">
<canvas id="canvas"></canvas>
</body>
<script>

var gl = canvas.getContext('experimental-webgl', { 'alpha': false });
var buffer = gl.createBuffer();
gl.bindBuffer(gl.ARRAY_BUFFER, buffer);
gl.bufferData(gl.ARRAY_BUFFER, new Float32Array([-1, -1, 1, -1, -1, 1, 1, 1]), gl.STATIC_DRAW);
gl.getExtension('OES_standard_derivatives');

function createShader(vertexSource, fragmentSource) {
  function compileShader(type, source) {
    var shader = gl.createShader(type);
    gl.shaderSource(shader, source);
    gl.compileShader(shader);
    var info = gl.getShaderInfoLog(shader);
    if (info) throw new Error(info);
    gl.attachShader(program, shader);
  }
  var program = gl.createProgram();
  compileShader(gl.VERTEX_SHADER, vertexSource);
  compileShader(gl.FRAGMENT_SHADER, fragmentSource);
  gl.linkProgram(program);
  return program;
}

function render() {
  gl.viewport(0, 0, gl.canvas.width, gl.canvas.height);
  gl.useProgram(shader);
  gl.enableVertexAttribArray(0);
  gl.vertexAttribPointer(0, 2, gl.FLOAT, false, 0, 0);

  var sinX = Math.sin(angleX);
  var cosX = Math.cos(angleX);
  var sinY = Math.sin(angleY);
  var cosY = Math.cos(angleY);
  gl.uniform3f(gl.getUniformLocation(shader, 'eye'), 0, 2, 0);
  gl.uniform3f(gl.getUniformLocation(shader, 'axisX'), cosY, 0, -sinY);
  gl.uniform3f(gl.getUniformLocation(shader, 'axisY'), sinY * sinX, cosX, cosY * sinX);
  gl.uniform3f(gl.getUniformLocation(shader, 'axisZ'), sinY * cosX, -sinX, cosY * cosX);
  gl.drawArrays(gl.TRIANGLE_STRIP, 0, 4);
}

function resize() {
  var dpr = window.devicePixelRatio || 1; // Support for retina displays
  gl.canvas.width = innerWidth * dpr;
  gl.canvas.height = innerHeight * dpr;
  gl.canvas.style.width = innerWidth + 'px';
  gl.canvas.style.height = innerHeight + 'px';
  render();
}

document.onmousedown = function(e) {
  e.preventDefault();
  var oldX = e.pageX;
  var oldY = e.pageY;

  document.onmousemove = function(e) {
    angleX += (e.pageY - oldY) * 0.001;
    angleY += (e.pageX - oldX) * 0.001;
    angleX = Math.max(-Math.PI / 2, Math.min(Math.PI / 2, angleX));
    oldX = e.pageX;
    oldY = e.pageY;
    render();
  };

  document.onmouseup = function(e) {
    document.onmousemove = document.onmouseup = null;
  };
};

var angleX = 0;
var angleY = 0;
var shader = createShader(vertexShader.textContent, fragmentShader.textContent);
window.onresize = resize;
resize();

</script>
